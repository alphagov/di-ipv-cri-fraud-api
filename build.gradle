plugins {
	id "java"
	id "org.sonarqube" version "3.3"
	id "com.diffplug.spotless" version "6.1.0"
}

ext {
	dependencyVersions = [
		jackson_version          : "2.13.1",
		aws_sdk_version          : "1.12.153",
		aws_lambda_core_version  : "1.2.1",
		aws_lambda_events_version: "3.11.0",
		aws_powertools_version   : "1.9.0",
		nimbusds_oauth_version   : "9.25",
		nimbusds_jwt_version     : "9.15.1",
		protobuf_version         : "3.19.4",
		junit                    : "5.8.2",
		glassfish_version        : "3.0.3",
	]
}

repositories {
	maven {
		url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
	}
}

subprojects {

	java {
		sourceCompatibility = JavaVersion.VERSION_11
		targetCompatibility = JavaVersion.VERSION_11
	}

	repositories {
		maven {
			url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
		}
	}

	configurations {
		dynamodb
		jackson
		lambda
		lambda_tests
		lettuce
		logging_runtime
		nimbus
		ssm
		tests
		test_runtime
		gson
	}

	dependencies {

		dynamodb "com.amazonaws:aws-java-sdk-dynamodb:${dependencyVersions.aws_sdk_version}",
				"software.amazon.awssdk:dynamodb-enhanced:2.17.116"

		gson "com.google.code.gson:gson:2.8.9"

		lambda "com.amazonaws:aws-lambda-java-core:${dependencyVersions.aws_lambda_core_version}",
				"com.amazonaws:aws-lambda-java-events:${dependencyVersions.aws_lambda_events_version}",
				"com.amazonaws:aws-lambda-java-log4j2:1.5.1",
				"software.amazon.lambda:powertools-parameters:${dependencyVersions.aws_powertools_version}"

		lambda_tests "com.amazonaws:aws-lambda-java-tests:1.1.1"

		jackson "com.fasterxml.jackson.core:jackson-core:${dependencyVersions.jackson_version}",
				"com.fasterxml.jackson.core:jackson-databind:${dependencyVersions.jackson_version}",
				"com.fasterxml.jackson.core:jackson-annotations:${dependencyVersions.jackson_version}",
				"com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${dependencyVersions.jackson_version}",
				"org.hibernate.validator:hibernate-validator:7.0.2.Final",
				"org.glassfish:jakarta.el:4.0.2",
				"jakarta.xml.bind:jakarta.xml.bind-api:3.0.1",
				"com.sun.xml.bind:jaxb-impl:3.0.2"

		nimbus "com.nimbusds:oauth2-oidc-sdk:${dependencyVersions.nimbusds_oauth_version}",
				"com.nimbusds:nimbus-jose-jwt:${dependencyVersions.nimbusds_jwt_version}"

		tests "org.junit.jupiter:junit-jupiter-api:${dependencyVersions.junit}",
				"org.junit.jupiter:junit-jupiter-params:${dependencyVersions.junit}",
				"org.mockito:mockito-junit-jupiter:4.3.1"

		test_runtime "org.junit.jupiter:junit-jupiter-engine:${dependencyVersions.junit}"
	}
}


sonarqube {
	properties {
		property "sonar.projectKey", "alphagov_di-ipv-cri-fraud-api"
		property "sonar.organization", "alphagov"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

spotless {
	java {
		target "**/src/**/*.java"
		googleJavaFormat("1.13.0").aosp()
		importOrder "", "javax", "java", "\\#"
		endWithNewline()
	}
	groovyGradle {
		target '**/*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

clean.doFirst {
	delete "${rootDir}/dist/"
	delete "${rootDir}/.aws-sam"
}
